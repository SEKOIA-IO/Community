rule apt_mustangpanda_payload {
    meta:
        id = "ce7ddf20-e13f-4b5f-8fff-4b1387b29568"
        version = "1.0"
        description = "Decryption routine of mustang panda payload"
        author = "Sekoia.io"
        creation_date = "2022-12-08"
        classification = "TLP:CLEAR"
        
    strings:
        $chunk_1 = {
        89 74 24 ??
        B9 ?? ?? ?? ??
        8B 44 24 ??
        3D ?? ?? ?? ??
        B8 ?? ?? ?? ??
        0F 4C C1
        E9 ?? ?? ?? ??
        B8 ?? ?? ?? ??
        31 DB
        31 ED
        31 FF
        E9 ?? ?? ?? ??
        8B 44 24 ??
        B9 ?? ?? ?? ??
        3B 44 24 ??
        B8 ?? ?? ?? ??
        0F 42 C1
        E9 ?? ?? ?? ??
        88 5C 24 ??
        89 6C 24 ??
        89 7C 24 ??
        B9 ?? ?? ?? ??
        8B 44 24 ??
        3D ?? ?? ?? ??
        B8 ?? ?? ?? ??
        0F 4C C1
        }
        
    condition:
        filesize < 8MB and all of them
}
        